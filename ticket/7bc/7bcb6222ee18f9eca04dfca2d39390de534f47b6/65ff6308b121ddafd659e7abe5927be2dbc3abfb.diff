--- wxwin.m4.old	2005-06-01 11:25:08.000000000 -0500
+++ wxwin.m4	2005-06-01 12:43:32.000000000 -0500
@@ -240,3 +240,98 @@
   AC_SUBST(WX_LIBS_STATIC)
   AC_SUBST(WX_VERSION)
 ])
+
+dnl ---------------------------------------------------------------------------
+dnl Get information on the wxrc program for making C++, Python and xrs
+dnl resource files.
+dnl
+dnl     AC_ARG_ENABLE(...)
+dnl     AC_ARG_WITH(...)
+dnl        ...
+dnl     AM_OPTIONS_WXCONFIG
+dnl     AM_OPTIONS_WXRC
+dnl        ...
+dnl     AM_PATH_WXCONFIG(2.6.0, wxWin=1)
+dnl     if test "$wxWin" != 1; then
+dnl        AC_MSG_ERROR([
+dnl                wxWidgets must be installed on your system
+dnl                but wx-config script couldn't be found.
+dnl
+dnl                Please check that wx-config is in path, the directory
+dnl                where wxWidgets libraries are installed (returned by
+dnl                'wx-config --libs' command) is in LD_LIBRARY_PATH or
+dnl                equivalent variable and wxWidgets version is 2.6.0 or above.
+dnl        ])
+dnl     fi
+dnl
+dnl     AM_PATH_WXRC([HAVE_WXRC=1], [HAVE_WXRC=0])
+dnl     if test "x$HAVE_WXRC" != x1; then
+dnl         AC_MSG_ERROR([
+dnl                The wxrc program was not installed or not found.
+dnl     
+dnl                Please check the wxWidgets installation.
+dnl         ])
+dnl     fi
+dnl
+dnl     CPPFLAGS="$CPPFLAGS $WX_CPPFLAGS"
+dnl     CXXFLAGS="$CXXFLAGS $WX_CXXFLAGS_ONLY"
+dnl     CFLAGS="$CFLAGS $WX_CFLAGS_ONLY"
+dnl
+dnl     LDFLAGS="$LDFLAGS $WX_LIBS"
+dnl ---------------------------------------------------------------------------
+
+dnl ---------------------------------------------------------------------------
+dnl AM_OPTIONS_WXRC
+dnl
+dnl adds support for --wxrc command line option
+dnl ---------------------------------------------------------------------------
+
+AC_DEFUN([AM_OPTIONS_WXRC],
+[
+    AC_ARG_WITH(wxrc,
+                [  --with-wxrc=WXRC        path to wxrc program to use],
+                wx_wxrc_name="$withval")
+])
+
+dnl ---------------------------------------------------------------------------
+dnl AM_PATH_WXRC([ACTION-IF-FOUND [, ACTION-IF-NOT-FOUND]])
+dnl
+dnl Test for wxWidgets' wxrc program for creating either C++, Python or XRS
+dnl resources.  The variable WXRC will be set and substituted in the configure
+dnl script and Makefiles.
+dnl
+dnl Example use:
+dnl   AM_PATH_WXRC([wxrc=1], [wxrc=0])
+dnl ---------------------------------------------------------------------------
+
+dnl
+dnl wxrc program from the wx-config script
+dnl
+AC_DEFUN([AM_PATH_WXRC],
+[
+  if test "x$WX_CONFIG_NAME" = x; then
+    AC_MSG_ERROR([The wxrc tests must run after wx-config.])
+  fi
+  
+  if test "x$wx_wxrc_name" != x ; then
+    AC_MSG_CHECKING([for wxrc ($wx_wxrc_name)])
+    if test -x $wx_wxrc_name ; then
+      AC_MSG_RESULT([yes])
+      ifelse([$1], , :, [$1])
+    else
+      AC_MSG_RESULT([not found])
+      ifelse([$2], , :, [$2])
+    fi
+  else
+    AC_MSG_CHECKING([for wxrc])
+    WXRC=`$WX_CONFIG_WITH_ARGS --utility=wxrc`
+    if test "x$WXRC" = x ; then
+      AC_MSG_RESULT([not found])
+      ifelse([$2], , :, [$2])
+    else
+      AC_MSG_RESULT($WXRC)
+      ifelse([$1], , :, [$1])
+    fi
+  fi
+  AC_SUBST(WXRC)
+])

 	  	 
