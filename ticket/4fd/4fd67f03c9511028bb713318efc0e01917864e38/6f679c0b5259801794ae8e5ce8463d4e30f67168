define pyframe
printf "-- pyframe --\n"
printf "function: %s()\n",((PyStringObject*)f->f_code->co_name)->ob_sval
printf "file    : %s:%i\n",((PyStringObject*)f->f_code->co_filename)->ob_sval,f->f_lineno
set $locals = ((PyTupleObject*)f->f_code->co_varnames)
if $locals != 0
    set $n = ((PyTupleObject*)f->f_code->co_varnames)->ob_size
    if $n > 0
        printf "-- locals --\n"
        set $i = 0
        while $i < $n
            printf "%s\n",((PyStringObject*)((PyTupleObject*)f->f_code->co_varnames)->ob_item[$i++])->ob_sval
        end
    end
end
end

document pyframe
Examine PyEvalFrame stack frames.
Calls to PyEvalFrame represent Python function calls. Pyframe will examine the
'f' argument to PyEvalFrame, which is a pointer to a PyFrameObject. It prints
the name of the Python function called, the name of the source file (including
line number) from which the function was imported, and it lists the names of
any local variables.
end
